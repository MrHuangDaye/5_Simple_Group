<values>

/showsummaryfeedback = true

</values>




<item attributeAlabel>

/1 = "积极"

</item>



<item attributeA>

/1 = "诚实"
/2 = "自信"
/3 = "可爱"
/4 = "伶俐"
/5 = "高贵"
/6 = "高尚"
/7 = "聪明"
/8 = "漂亮"
/9 = "成功"
/10 = "强壮"

</item>



<item attributeBlabel>

/1 = "消极"

</item>



<item attributeB>

/1 = "罪恶"
/2 = "卑鄙"
/3 = "无能"
/4 = "愚蠢"
/5 = "失败"
/6 = "讨厌"
/7 = "笨拙"
/8 = "可恨"
/9 = "陈腐"
/10 = "丑陋"

</item>



<item targetAlabel>

/1 = "自我"

</item>



<item targetA>

/1 = "俺"
/2 = "俺的"
/3 = "自己"
/4 = "自己的"
/5 = "自个"
/6 = "自个的"
/7 = "我"
/8 = "我的"
/9 = "本人"
/10 = "本人的"

</item>



<item targetBlabel>

/1 = "他人"

</item>



<item targetB>

/1 = "他"
/2 = "他的"
/3 = "别人"
/4 = "别人的"
/5 = "人家"
/6 = "人家的"
/7 = "外人"
/8 = "外人的"
/9 = "他人"
/10 = "他人的"

</item>




<defaults>

/ fontstyle = ("Arial", 3.5%)

/ screencolor = (0,0,0)

/ txbgcolor = (0,0,0)

/ txcolor = (255, 255, 255)

/ minimumversion = "4.0.0.0"

/ canvasaspectratio = (4, 3)

</defaults>



<values>

/ completed = 0

/ sum1a = 0

/ sum2a = 0

/ sum1b = 0

/ sum2b = 0

/ n1a = 0

/ n2a = 0

/ n1b = 0

/ n2b = 0

/ ss1a = 0

/ ss2a = 0

/ ss1b = 0

/ ss2b = 0

/ magnitude = "unknown"

/ n_correct = 0

</values>




<expressions>

/ m1a = values.sum1a / values.n1a

/ m2a = values.sum2a / values.n2a

/ m1b = values.sum1b / values.n1b

/ m2b = values.sum2b / values.n2b

/ sd1a = sqrt((values.ss1a - (values.n1a * (expressions.m1a * expressions.m1a))) / (values.n1a - 1))

/ sd2a = sqrt((values.ss2a - (values.n2a * (expressions.m2a * expressions.m2a))) / (values.n2a - 1))

/ sd1b = sqrt((values.ss1b - (values.n1b * (expressions.m1b * expressions.m1b))) / (values.n1b - 1))

/ sd2b = sqrt((values.ss2b - (values.n2b * (expressions.m2b * expressions.m2b))) / (values.n2b - 1))

/ sda = sqrt((((values.n1a - 1) * (expressions.sd1a * expressions.sd1a) + (values.n2a - 1) * (expressions.sd2a * expressions.sd2a)) + ((values.n1a + values.n2a) * ((expressions.m1a - expressions.m2a) * (expressions.m1a - expressions.m2a)) / 4) ) / (values.n1a + values.n2a - 1) )

/ sdb = sqrt((((values.n1b - 1) * (expressions.sd1b * expressions.sd1b) + (values.n2b - 1) * (expressions.sd2b * expressions.sd2b)) + ((values.n1b + values.n2b) * ((expressions.m1b - expressions.m2b) * (expressions.m1b - expressions.m2b)) / 4) ) / (values.n1b + values.n2b - 1) )

/ da = (m2a - m1a) / expressions.sda

/ db = (m2b - m1b) / expressions.sdb

/ d = (expressions.da + expressions.db) / 2

/ preferred = "unknown"

/ notpreferred = "unknown"

/ percentcorrect = (values.n_correct/ (values.n1a + values.n1b + values.n2a + values.n2b)) * 100

</expressions>







<data>

/ file = "neiyinzizunIAT_rawdata.iqdat"

/ columns = [date, time, group, subject, blockcode, blocknum, trialcode, trialnum, response, correct, latency, 

stimulusnumber, stimulusitem, expressions.da, expressions.db, expressions.d, expressions.percentcorrect ]

/ separatefiles = true

</data>







<trial instructions>

/ stimulustimes = [1=instructions, spacebar]

/ correctresponse = (" ")

/ errormessage = false

/ recorddata = false

</trial>



<text instructions>

/ items = instructions

/ hjustify = left

/ size = (90%, 60%)

/ position = (50%, 85%)

/ valign = bottom

/ select = sequence

/ resetinterval = 20

</text>



<item instructions>

/ 1 = "

练习部分

您好，欢迎参加我们的实验！

在接下来的实验中，屏幕中央将会呈现一系列的词语，你的任务是对词语进行辨别归类并按键反应。如果词语属于左侧词语描述的范畴之内，请按“F”键；如果词语属于右侧词语描述的范畴之内，请按“J”键。请尽量快速准确的做出反应。
"
/ 2 = "

练习部分

如屏幕上方所示，词语位置已经改变，接下来呈现的词汇也将改变。

按键规则不变。
"
/ 3 = "

练习部分

如屏幕上方所示，词语已经改变，接下来呈现的词汇也将改变。

按键规则不变。
"

/ 4 = "

练习部分

如屏幕上方所示，词语位置已经改变，接下来呈现的词汇也将改变。

按键规则不变。
"
/ 5 = "

练习部分

如屏幕上方所示，词语现已进行分组，请根据分组，将接下来呈现的词汇进行归类。

按键规则不变。
"
/ 6 = "

正式实验

如屏幕上方所示，请根据词语分组，将接下来呈现的词汇进行归类。

按键规则不变。
"
/ 7 = "

练习部分

如屏幕上方所示，词语分组已经改变，请根据分组，将接下来呈现的词汇进行归类。

按键规则不变。
"
/ 8 ="

正式实验

如屏幕上方所示，请根据词语分组，将接下来呈现的词汇进行归类。

按键规则不变。
"
/ 9 ="实验结束，感谢您参与我们的实验！"
</item>



<text spacebar>

/ items = ("请按空格键继续")

/ position = (50%, 95%)

/ valign = bottom

</text>



<text attributeA>

/ items = attributeA

/ fontstyle = ("Arial", 5%)

/ txcolor = (0, 255, 0)

</text>



<text attributeB>

/ items = attributeB

/ fontstyle = ("Arial", 5%)

/ txcolor = (0, 255, 0)

</text>



<text targetB>

/ items = targetB

/ fontstyle = ("Arial", 5%)

</text>



<text targetA>

/ items = targetA

/ fontstyle = ("Arial", 5%)

</text>



<text error>

/ position = (50%, 75%)

/ items = ("X")

/ color = (255, 0, 0)

/ fontstyle = ("Arial", 10%, true)

</text>



<text attributeAleft>

/ items = attributeAlabel

/ valign = top

/ halign = left

/ position = (5%, 5%)

/ txcolor = (0, 255, 0)

/ fontstyle = ("Arial", 5%)

</text>


<text attributeAright>

/ items = attributeAlabel

/ valign = top

/ halign = right

/ position = (95%, 5%)

/ txcolor = (0, 255, 0)

/ fontstyle = ("Arial", 5%)

</text>


<text attributeBleft>

/ items = attributeBlabel

/ valign = top

/ halign = left

/ position = (5%, 5%)

/ txcolor = (0, 255, 0)

/ fontstyle = ("Arial", 5%)

</text>


<text attributeBright>

/ items = attributeBlabel

/ valign = top

/ halign = right

/ position = (95%, 5%)

/ txcolor = (0, 255, 0)

/ fontstyle = ("Arial", 5%)

</text>



<text attributeAleftmixed>

/ items = attributeAlabel

/ valign = top

/ halign = left

/ position = (5%, 19%)

/ txcolor = (0, 255, 0)

/ fontstyle = ("Arial", 5%)

</text>



<text attributeBrightmixed>

/ items = attributeBlabel

/ valign = top

/ halign = right

/ position = (95%, 19%)

/ txcolor = (0, 255, 0)

/ fontstyle = ("Arial", 5%)

</text>



<text targetBleft>

/ items = targetBlabel

/ valign = top

/ halign = left	

/ position = (5%, 5%)

/ fontstyle = ("Arial", 5%)

</text>



<text targetBright>

/ items = targetBlabel

/ valign = top

/ halign = right

/ position = (95%, 5%)

/ fontstyle = ("Arial", 5%)

</text>



<text targetAleft>

/ items = targetAlabel

/ valign = top

/ halign = left

/ position = (5%, 5%)

/ fontstyle = ("Arial", 5%)

</text>



<text targetAright>

/ items = targetAlabel

/ valign = top

/ halign = right

/ position = (95%, 5%)

/ fontstyle = ("Arial", 5%)

</text>



<text orleft>

/ items = ("or")

/ valign = top

/ halign = left

/ position = (5%, 12%)

/ fontstyle = ("Arial", 5%)

</text>



<text orright>

/ items = ("or")

/ valign = top

/ halign = right

/ position = (95%, 12%)

/ fontstyle = ("Arial", 5%)

</text>

<trial attributeA>

/ validresponse = ("F", "J")

/ correctresponse = ("F")

/ stimulusframes = [1 = attributeA]

/ posttrialpause = 250

</trial>



<trial attributeAright>

/ validresponse = ("F", "J")

/ correctresponse = ("J")

/ stimulusframes = [1 = attributeA]

/ posttrialpause = 250

</trial>



<trial attributeB>

/ validresponse = ("F", "J")

/ correctresponse = ("J")

/ stimulusframes = [1 = attributeB]

/ posttrialpause = 250

</trial>


<trial attributeBleft>

/ validresponse = ("F", "J")

/ correctresponse = ("F")

/ stimulusframes = [1 = attributeB]

/ posttrialpause = 250

</trial>


<trial targetBleft>

/ validresponse = ("F", "J")

/ correctresponse = ("F")

/ stimulusframes = [1 = targetB]

/ posttrialpause = 250

</trial>



<trial targetBright>

/ validresponse = ("F", "J")

/ correctresponse = ("J")

/ stimulusframes = [1 = targetB]

/ posttrialpause = 250

</trial>



<trial targetAleft>

/ validresponse = ("F", "J")

/ correctresponse = ("F")

/ stimulusframes = [1 = targetA]

/ posttrialpause = 250

</trial>



<trial targetAright>

/ validresponse = ("F", "J")

/ correctresponse = ("J")

/ stimulusframes = [1 = targetA]

/ posttrialpause = 250

</trial>



<block attributepractice>

/ bgstim = (attributeAleft, attributeBright)

/ trials = [1=instructions;2-21 = random(attributeA, attributeB)]

/ errormessage = true(error,200)

/ responsemode = correct

</block>


<block attributepracticejiaohuan>

/ bgstim = (attributeAright, attributeBleft)

/ trials = [1=instructions;2-21 = random(attributeAright, attributeBleft)]

/ errormessage = true(error,200)

/ responsemode = correct

</block>


<block targetcompatiblepractice>

/ bgstim = (targetAleft, targetBright)

/ trials = [1=instructions;2-21 = random(targetAleft, targetBright)]

/ errormessage = true(error,200)

/ responsemode = correct

</block>



<block targetincompatiblepractice>

/ bgstim = (targetAright, targetBleft)

/ trials = [1=instructions;2-21 = random(targetAright, targetBleft)]

/ errormessage = true(error,200)

/ responsemode = correct

</block>



<block compatibletest1>

/ bgstim = (targetAleft, orleft, attributeAleftmixed, targetBright, orright, attributeBrightmixed)

/ trials = [1=instructions;

  3,5,7,9,11,13,15,17,19,21= random(targetAleft, targetBright);

  2,4,6,8,10,12,14,16,18,20 = random(attributeA, attributeB)]

/ errormessage = true(error,200)

/ responsemode = correct

/ ontrialend = [if(block.compatibletest1.latency  <= 10000 && block.compatibletest1.currenttrialnumber != 1 ) values.sum1a =  values.sum1a + block.compatibletest1.latency]

/ ontrialend = [if(block.compatibletest1.latency  <= 10000 && block.compatibletest1.currenttrialnumber != 1 ) values.n1a +=  1]

/ ontrialend = [if(block.compatibletest1.latency  <= 10000 && block.compatibletest1.currenttrialnumber != 1 ) values.ss1a =  values.ss1a + (block.compatibletest1.latency * block.compatibletest1.latency)]

/ ontrialend = [if(block.compatibletest1.latency  <= 10000 && block.compatibletest1.currenttrialnumber != 1 ) values.n_correct += block.compatibletest1.correct]

</block>



<block compatibletest2>

/ bgstim = (targetAleft, orleft, attributeAleftmixed, targetBright, orright, attributeBrightmixed)

/ trials = [

  2,4,6,8,10,12,14,16,18,20,22,24,26,28,30,32,34,36,38,40 = random(targetAleft, targetBright);

  1,3,5,7,9,11,13,15,17,19,21,23,25,27,29,31,33,35,37,39 = random(attributeA, attributeB)]

/ errormessage = true(error,200)

/ responsemode = correct

/ ontrialend = [if(block.compatibletest2.latency  <= 10000) values.sum1b =  values.sum1b + block.compatibletest2.latency]

/ ontrialend = [if(block.compatibletest2.latency  <= 10000) values.n1b += 1]

/ ontrialend = [if(block.compatibletest2.latency  <= 10000) values.ss1b =  values.ss1b + (block.compatibletest2.latency * block.compatibletest2.latency)]

/ ontrialend = [if(block.compatibletest2.latency  <= 10000) values.n_correct += block.compatibletest2.correct]

</block>



<block incompatibletest1>

/ bgstim = (targetBleft, orleft, attributeAleftmixed, targetAright, orright, attributeBrightmixed)

/ trials = [1=instructions;

  3,5,7,9,11,13,15,17,19,21 = random(targetBleft, targetAright);

  2,4,6,8,10,12,14,16,18,20 = random(attributeA, attributeB)]

/ errormessage = true(error,200)

/ responsemode = correct

/ ontrialend = [if(block.incompatibletest1.latency  <= 10000 && block.incompatibletest1.currenttrialnumber != 1) values.sum2a =  values.sum2a + block.incompatibletest1.latency]

/ ontrialend = [if(block.incompatibletest1.latency  <= 10000 && block.incompatibletest1.currenttrialnumber != 1 ) values.n2a +=  1]

/ ontrialend = [if(block.incompatibletest1.latency  <= 10000 && block.incompatibletest1.currenttrialnumber != 1 ) values.ss2a =  values.ss2a + (block.incompatibletest1.latency * block.incompatibletest1.latency)]

/ ontrialend = [if(block.incompatibletest1.latency  <= 10000 && block.incompatibletest1.currenttrialnumber != 1 ) values.n_correct += block.incompatibletest1.correct]

</block>



<block incompatibletest2>

/ bgstim = (targetBleft, orleft, attributeAleftmixed, targetAright, orright, attributeBrightmixed)

/ trials = [

  2,4,6,8,10,12,14,16,18,20,22,24,26,28,30,32,34,36,38,40 = random(targetBleft, targetAright);

  1,3,5,7,9,11,13,15,17,19,21,23,25,27,29,31,33,35,37,39 = random(attributeA, attributeB)]

/ errormessage = true(error,200)

/ responsemode = correct

/ ontrialend = [if(block.incompatibletest2.latency  <= 10000) values.sum2b =  values.sum2b + block.incompatibletest2.latency]

/ ontrialend = [if(block.incompatibletest2.latency  <= 10000) values.n2b +=  1]

/ ontrialend = [if(block.incompatibletest2.latency  <= 10000) values.ss2b =  values.ss2b + (block.incompatibletest2.latency * block.incompatibletest2.latency)]

/ ontrialend = [if(block.incompatibletest2.latency  <= 10000) values.n_correct += block.incompatibletest2.correct]

</block>





<block compatibletestinstructions>

/ bgstim = (targetAleft, orleft, attributeAleftmixed, targetBright, orright, attributeBrightmixed)

/ trials = [1=instructions]

/ recorddata = false

</block>



<block incompatibletestinstructions>

/ bgstim = (targetBleft, orleft, attributeAleftmixed, targetAright, orright, attributeBrightmixed)

/ trials = [1=instructions]

/ recorddata = false

</block>



<block summary>


/ trials = [1=instructions]

/ recorddata = false

</block>


<expt>

/subjects = (1 of 2)

/groupassignment = groupnumber

/ blocks = [1=targetcompatiblepractice; 2=targetincompatiblepractice; 3=attributepractice; 4=attributepracticejiaohuan;

				5=compatibletest1; 6=compatibletestinstructions; 7=compatibletest2; 8=incompatibletest1;

				9=incompatibletestinstructions; 10=incompatibletest2; 11=summary]

/onexptend = [values.completed = 1]

</expt>



<expt>

/subjects = (2 of 2)

/groupassignment = groupnumber

/ blocks =[1=targetcompatiblepractice; 2=targetincompatiblepractice; 3=attributepractice; 4=attributepracticejiaohuan;

				5=incompatibletest1;

				6=incompatibletestinstructions; 7=incompatibletest2;8=compatibletest1; 9=compatibletestinstructions; 10=compatibletest2; 11=summary]


/onexptend = [values.completed = 1]

</expt>
